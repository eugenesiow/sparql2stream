PREFIX sao: <http://purl.oclc.org/NET/sao/>
PREFIX ssn: <http://purl.oclc.org/NET/ssnx/ssn#>
PREFIX ct: 	<http://www.insight-centre.org/citytraffic#>
PREFIX ns: 	<http://www.insight-centre.org/dataset/SampleEventService#>

SELECT ?title ?lat1 ?lon1 ?lat2 ?lon2 ?v2
FROM NAMED WINDOW :traffic ON <http://www.insight-centre.org/dataset/SampleEventService#AarhusTrafficData158505> [RANGE 3s]
FROM NAMED <http://www.insight-centre.org/dataset/SampleEventService#AarhusCulturalEvents>
FROM NAMED <http://www.insight-centre.org/dataset/SampleEventService#SensorRepository>
WHERE {
	GRAPH ns:SensorRepository {
		?p2 a ct:CongestionLevel;
			ssn:isPropertyOf ?foi2.
		?foi2 ct:hasStartLatitude ?lat2;
			ct:hasStartLongitude ?lon2.
	}
	GRAPH ns:AarhusCulturalEvents {
		?evtId a ssn:Observation;
			sao:value ?title;
			ssn:featureOfInterest ?foi.
		?foi ct:hasFirstNode ?node.
		?node ct:hasLatitude ?lat1;
			ct:hasLongitude ?lon1.
	}
	WINDOW :traffic {
		?obId2 a ssn:Observation;
			ssn:observedProperty ?p2;
			sao:hasValue ?v2;
			ssn:observedBy ns:AarhusTrafficData158505.
	}
	FILTER (((?lat2-?lat1)*(?lat2-?lat1)+(?lon2-?lon1)*(?lon2-?lon1))<0.1)
}